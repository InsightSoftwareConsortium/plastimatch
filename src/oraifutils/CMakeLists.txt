CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(ORAIFUTILS CXX)

# Library version information
SET(ORAIFUTILS_MAJOR_VERSION 1)
SET(ORAIFUTILS_MINOR_VERSION 1)
SET(ORAIFUTILS_PATCH_VERSION 0)
SET(ORAIFUTILS_VERSION ${ORAIFUTILS_MAJOR_VERSION}.${ORAIFUTILS_MINOR_VERSION}.${ORAIFUTILS_PATCH_VERSION})

# Require ITK 3.20
SET(ITK_MIN_VERSION "3.20")
FIND_PACKAGE(ITK) 
IF(NOT ITK_FOUND)
  MESSAGE(SEND_ERROR "ITK ${ITK_MIN_VERSION} not found.  Please set the ITK_DIR variable.")
ELSE(NOT ITK_FOUND)
  # The version check fails with cmake 2.8.4+linux(win untested), manually perform check
  IF("${ITK_VERSION_MAJOR}.${ITK_VERSION_MINOR}" VERSION_LESS ${ITK_MIN_VERSION})
    MESSAGE(SEND_ERROR "ITK ${ITK_MIN_VERSION} not found (version ${ITK_VERSION_MAJOR}.${ITK_VERSION_MINOR} provided).  Please set the ITK_DIR variable.")
  ELSE("${ITK_VERSION_MAJOR}.${ITK_VERSION_MINOR}" VERSION_LESS ${ITK_MIN_VERSION})
    INCLUDE(${ITK_USE_FILE})
  ENDIF("${ITK_VERSION_MAJOR}.${ITK_VERSION_MINOR}" VERSION_LESS ${ITK_MIN_VERSION})
ENDIF(NOT ITK_FOUND)

# Require VTK 5.6
SET(VTK_MIN_VERSION "5.6.0")
FIND_PACKAGE(VTK ${VTK_MIN_VERSION})
IF(NOT VTK_FOUND)
  MESSAGE(SEND_ERROR "VTK ${VTK_MIN_VERSION} not found.  Please set the VTK_DIR variable.")
ELSE(NOT VTK_FOUND)
  INCLUDE(${VTK_USE_FILE})
ENDIF(NOT VTK_FOUND)

# Add Option to enable Qt Support and build Qt dependent libraries
OPTION(ORAIFUTILS_USE_QT "Build parts of ORAIFUTILS with Qt dependencies" OFF)
IF(ORAIFUTILS_USE_QT)
  # Require Qt 4.6.0 or later
  SET(QT_MIN_VERSION "4.6.0")
  FIND_PACKAGE(Qt4 ${QT_MIN_VERSION})
  IF(NOT QT4_FOUND)
    MESSAGE(SEND_ERROR "Qt ${QT_MIN_VERSION} or greater not found.  Please set the QT_QMAKE_EXECUTABLE variable.")
  ELSE(NOT QT4_FOUND)
    INCLUDE(${QT_USE_FILE})
  ENDIF(NOT QT4_FOUND)
ENDIF(ORAIFUTILS_USE_QT)

# Build static libraries by default
OPTION(BUILD_SHARED_LIBS "Build ORAIFUTILS with shared libraries." OFF)

# Offer the user the choice of overriding the installation directories
SET(INSTALL_LIB_DIR lib CACHE PATH "Installation directory for libraries")
SET(INSTALL_BIN_DIR bin CACHE PATH "Installation directory for executables")
SET(INSTALL_INCLUDE_DIR include CACHE PATH "Installation directory for header files")
SET(INSTALL_DATA_DIR share CACHE PATH "Installation directory for data files")

# Make relative paths absolute (needed later on)
FOREACH(p LIB BIN INCLUDE DATA)
  SET(var INSTALL_${p}_DIR)
  IF(NOT IS_ABSOLUTE "${${var}}")
    SET(${var} "${CMAKE_INSTALL_PREFIX}/${${var}}")
  ENDIF()
ENDFOREACH()

# Build unit tests
OPTION(ORAIFUTILS_BUILD_TESTING "Build ORAIF unit tests for regression tests and automatic software verification." OFF)
IF(ORAIFUTILS_BUILD_TESTING)
  ENABLE_TESTING()
ENDIF(ORAIFUTILS_BUILD_TESTING)

# set up include-directories
INCLUDE_DIRECTORIES(
  "${PROJECT_SOURCE_DIR}"   
  "${PROJECT_BINARY_DIR}")  

# Add sub-directories
SET(ORAIFUTILS_BASICTESTING_SUBDIR testing)
SET(ORAIFUTILS_TOOLS_SUBDIR tools)
SET(ORAIFUTILS_MVC_SUBDIR mvc)
SET(ORAIFUTILS_STRUCTUREACCESS_SUBDIR structureaccess)
SET(ORAIFUTILS_TRANSFORM_SUBDIR transform)
SET(ORAIFUTILS_REGISTRATION_SUBDIR registration)
SET(ORAIFUTILS_IMAGEACCESS_SUBDIR imageaccess)
SET(ORAIFUTILS_IMAGETOOLS_SUBDIR imagetools)
SET(ORAIFUTILS_MODEL_SUBDIR model)
SET(ORAIFUTILS_NREG2D3D_SUBDIR nreg2d3d)
SET(ORAIFUTILS_GUICOMPONENTS_SUBDIR guicomponents)
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_BASICTESTING_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_TOOLS_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_MVC_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_STRUCTUREACCESS_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_TRANSFORM_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_REGISTRATION_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_IMAGEACCESS_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_IMAGETOOLS_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_MODEL_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_NREG2D3D_SUBDIR})
ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/${ORAIFUTILS_GUICOMPONENTS_SUBDIR})

# Add all targets to the build-tree export set
SET(ORAIFUTILS_LIBRARIES
  ORAIFTools
  ORAIFMVC
  ORAIFStructureAccess
  ORAIFTransform
  ORAIFRegistration
  ORAIFImageAccess
  ORAIFImageTools
  ORAIFModel
  ORAIFNReg2D3DMathMethods
  ORAIFNReg2D3DDRREngine
  ORAIFNReg2D3DInterpolation
  ORAIFNReg2D3DTransform
  ORAIFNReg2D3DMetrics
  ORAIFNReg2D3DAlgorithm
  ORAIFGUIComponents
)
EXPORT(TARGETS ${ORAIFUTILS_LIBRARIES}
  FILE "${PROJECT_BINARY_DIR}/ORAIFUTILSLibraryDepends.cmake")

# Export the package for use from the build-tree
# (this registers the build-tree with a global CMake-registry)
EXPORT(PACKAGE ORAIFUTILS)

# Create a ORAIFUTILSConfig.cmake file for the use from the build tree
SET(ORAIFUTILS_INCLUDE_DIRS "${PROJECT_SOURCE_DIR}" "${PROJECT_BINARY_DIR}" 
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_BASICTESTING_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_TOOLS_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_MVC_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_STRUCTUREACCESS_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_TRANSFORM_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_REGISTRATION_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_IMAGEACCESS_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_IMAGETOOLS_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_MODEL_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_NREG2D3D_MATHS_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_NREG2D3D_DRRENGINE_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_NREG2D3D_INTERPOLATION_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_NREG2D3D_TRANSFORM_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_NREG2D3D_METRIC_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_NREG2D3D_ALGORITHM_SUBDIR}"
  "${CMAKE_SOURCE_DIR}/${ORAIFUTILS_GUICOMPONENTS_SUBDIR}"
)
SET(ORAIFUTILS_LIB_DIR 
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_TOOLS_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_MVC_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_STRUCTUREACCESS_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_TRANSFORM_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_REGISTRATION_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_IMAGEACCESS_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_IMAGETOOLS_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_MODEL_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_NREG2D3D_MATHS_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_NREG2D3D_DRRENGINE_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_NREG2D3D_INTERPOLATION_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_NREG2D3D_TRANSFORM_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_NREG2D3D_METRIC_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_NREG2D3D_ALGORITHM_SUBDIR}"
  "${PROJECT_BINARY_DIR}/${ORAIFUTILS_GUICOMPONENTS_SUBDIR}"
)
SET(ORAIFUTILS_CMAKE_DIR "${PROJECT_BINARY_DIR}")
CONFIGURE_FILE(ORAIFUTILSConfig.cmake.in "${PROJECT_BINARY_DIR}/ORAIFUTILSConfig.cmake" @ONLY)
CONFIGURE_FILE(ORAIFUTILSConfigVersion.cmake.in "${PROJECT_BINARY_DIR}/ORAIFUTILSConfigVersion.cmake" @ONLY)

# Install the export set for use with the install-tree
INSTALL(EXPORT ORAIFUTILSLibraryDepends DESTINATION
  "${INSTALL_DATA_DIR}/ORAIFUTILS/CMake"
  COMPONENT dev)

# Create a ORAIFUTILSConfig.cmake file for the use from the install tree
# and install it
SET(ORAIFUTILS_INCLUDE_DIRS "${INSTALL_INCLUDE_DIR}" 
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_BASICTESTING_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_TOOLS_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_MVC_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_STRUCTUREACCESS_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_TRANSFORM_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_REGISTRATION_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_IMAGEACCESS_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_IMAGETOOLS_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_MODEL_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_NREG2D3D_MATHS_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_NREG2D3D_DRRENGINE_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_NREG2D3D_INTERPOLATION_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_NREG2D3D_TRANSFORM_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_NREG2D3D_METRIC_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_NREG2D3D_ALGORITHM_SUBDIR}"
  "${INSTALL_INCLUDE_DIR}/${ORAIFUTILS_GUICOMPONENTS_SUBDIR}"
)
SET(ORAIFUTILS_LIB_DIR "${INSTALL_LIB_DIR}")
SET(ORAIFUTILS_CMAKE_DIR "${INSTALL_DATA_DIR}/ORAIFUTILS/CMake")
CONFIGURE_FILE(ORAIFUTILSConfig.cmake.in
  "${PROJECT_BINARY_DIR}/InstallFiles/ORAIFUTILSConfig.cmake" @ONLY)
CONFIGURE_FILE(ORAIFUTILSConfigVersion.cmake.in
  "${PROJECT_BINARY_DIR}/InstallFiles/ORAIFUTILSConfigVersion.cmake" @ONLY)
INSTALL(FILES
  "${PROJECT_BINARY_DIR}/InstallFiles/ORAIFUTILSConfig.cmake"
  "${PROJECT_BINARY_DIR}/InstallFiles/ORAIFUTILSConfigVersion.cmake"
  DESTINATION "${ORAIFUTILS_CMAKE_DIR}" COMPONENT dev)

